" ---------
" Functions
" ---------

" -------
" OpenURL
" -------
if has('ruby')
ruby << EOF
  #encoding: utf-8
  require 'open-uri'
  require 'openssl'

  def extract_url(url)
    re = %r{(?i)\b((?:[a-z][\w-]+:(?:/{1,3}|[a-z0-9%])|www\d{0,3}[.]|
    [a-z0-9.\-]+[.][a-z]{2,4}/)(?:[^\s()<>]+|\(([^\s()<>]+|(\([^\s()<>]
    +\)))*\))+(?:\(([^\s()<>]+|(\([^\s()<>]+\)))*\)|[^\s`!()\[\]\{\};:'"
    .,<>?«»“”‘’]))}

    url.match(re).to_s
  end

  def open_url
    line = VIM::Buffer.current.line
    if url = extract_url(line)
      if RUBY_PLATFORM.downcase =~ /(win|mingw)(32|64)/
        `start cmd /c chrome #{url}`
        VIM::message("Opened #{url}")
      else
        `open #{url}`
        VIM::message("Opened #{url}")
      end
    else
      VIM::message("No URL found on this line.")
    end
  end

EOF

  " ----------
  " Open a URL
  " ----------
  if !exists("*OpenURL")
    function! OpenURL()
      :ruby open_url
    endfunction
  endif

  command! OpenUrl call OpenURL()
  nnoremap <leader>o :call OpenURL()<CR>

endif " endif has('ruby')

" ------------------------------------------
" Quick spelling fix (first item in z= list)
" ------------------------------------------
function! QuickSpellingFix()
  if &spell
    normal 1z=
  else
    " Enable spelling mode and do the correction
    set spell
    normal 1z=
    set nospell
  endif
endfunction

command! QuickSpellingFix call QuickSpellingFix()
nnoremap <silent> <leader>z :QuickSpellingFix<CR>

" -------------------------
" Write Buffer if Necessary
" -------------------------
" Writes the current buffer if it's needed, unless we're the in QuickFix mode.
function! WriteBufferIfNecessary()
  if &modified && !&readonly
    :write
  endif
endfunction
command! WriteBufferIfNecessary call WriteBufferIfNecessary()

function! CRWriteIfNecessary()
  if &filetype == "qf"
    " Execute a normal enter when in Quickfix list.
    execute "normal! \<enter>"
  else
    :WriteBufferIfNecessary
  endif
endfunction

" Clear the search buffer when hitting return
" Idea for MapCR from http://git.io/pt8kjA
function! MapCR()
  nnoremap <silent> <enter> :call CRWriteIfNecessary()<CR>
endfunction
call MapCR()

" ---------------------------------------------------------
" Make a scratch buffer with all of the leader keybindings.
" ---------------------------------------------------------
" Adapted from http://ctoomey.com/posts/an-incremental-approach-to-vim/
function! ListLeaders()
  silent! redir @b
  silent! nmap <LEADER>
  silent! redir END
  silent! new
  silent! set buftype=nofile
  silent! set bufhidden=hide
  silent! setlocal noswapfile
  silent! put! b
  silent! g/^s*$/d
  silent! %s/^.*,//
  silent! normal ggVg
  silent! sort
  silent! let lines = getline(1,"$")
  silent! normal <esc>
endfunction

command! ListLeaders :call ListLeaders()

" function! CopyMatches(reg)
"   let hits = []
"   %s//\=len(add(hits, submatch(0))) ? submatch(0) : ''/ge
"   let reg = empty(a:reg) ? '+' : a:reg
"   execute 'let @'.reg.' = join(hits, "\n") . "\n"'
" endfunction
" command! -register CopyMatches call CopyMatches(<q-reg>)
"
" function! YankLineWithoutNewline()
"   let l = line(".")
"   let c = col(".")
"   normal ^y$
"   " Clean up: restore previous search history, and cursor position
"   call cursor(l, c)
" endfunction
"
" nnoremap <silent>yl :call YankLineWithoutNewline()<CR>

" Show the word frequency of the current buffer in a split.
" from: http://vim.wikia.com/wiki/Word_frequency_statistics_for_a_file
function! WordFrequency() range
  let all = split(join(getline(a:firstline, a:lastline)), '\A\+')
  let frequencies = {}
  for word in all
    let frequencies[word] = get(frequencies, word, 0) + 1
  endfor
  new
  setlocal buftype=nofile bufhidden=hide noswapfile tabstop=20
  for [key,value] in items(frequencies)
    call append('$', key."\t".value)
  endfor
  sort i
endfunction
command! -range=% WordFrequency <line1>,<line2>call WordFrequency()

" Clean all end of line extra whitespace with ,S
" Credit: voyeg3r https://github.com/mitechie/pyvim/issues/#issue/1
" deletes excess space but maintains the list of jumps unchanged
" for more details see: h keepjumps
fun! s:CleanExtraSpaces()
    let save_cursor = getpos(".")
    let old_query = getreg('/')
    :%s/\s\+$//e
    call setpos('.', save_cursor)
    call setreg('/', old_query)
endfun
noremap <silent><leader>S <esc>:keepjumps call <SID>CleanExtraSpaces()<cr>

" ----------------------
" Zoom / Restore window.
" ----------------------
function! s:ZoomToggle() abort
    if exists('t:zoomed') && t:zoomed
        execute t:zoom_winrestcmd
        let t:zoomed = 0
    else
        let t:zoom_winrestcmd = winrestcmd()
        resize
        vertical resize
        let t:zoomed = 1
    endif
endfunction
command! ZoomToggle call s:ZoomToggle()
nnoremap <silent><leader>Z  :ZoomToggle<CR>

" --------------------------------------------------------
" To highlight all character that are in virtual column 80
" --------------------------------------------------------
function! s:HighLight80()
    highlight column80 term=bold ctermfg=red guifg=red
    match column80 /\%<81v.\%>80v/
endfunction
command! HL80 call s:HighLight80()

